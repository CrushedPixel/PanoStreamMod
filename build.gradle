buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sponge"
            url = "https://repo.spongepowered.org/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}

repositories {
    maven {
        name = "SpongePowered Repo"
        url = "http://repo.spongepowered.org/maven/"
    }
    maven {
        url = "https://repo.spacehq.org/content/repositories/snapshots/"
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'

sourceCompatibility = 1.8
targetCompatibility = 1.8

version = "1.0"
group= "com.replaymod"
archivesBaseName = "panostream"

ext {
    mixinSrg = new File(project.buildDir, 'tmp/mixins/mixins.srg')
    mixinRefMap = new File(project.buildDir, 'tmp/mixins/mixins.panostream.refmap.json')
    libFiles = fileTree(dir: 'libs', includes: ['*.jar'])
}

minecraft {
    coreMod = "com.replaymod.panostream.coremod.LoadingPlugin"
    version = "1.12.2-14.23.5.2795"
    runDir = "eclipse"
    mappings = "stable_39"
}

dependencies {
    compile 'org.projectlombok:lombok:1.16.4'
    compile 'org.spongepowered:mixin:0.7.10-SNAPSHOT'

    compile project(':jGui')
    compile project(':SimpleConfig')
}

def getGitCommitHash() {
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--always', '--dirty=*'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    } catch (e) {
        return "unknown"
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version + '-' + getGitCommitHash(), 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    dependsOn configurations.runtime

    def shade = {files(configurations.runtime.findAll { c ->
        ['mixin', 'SimpleConfig', 'jGui'].any {c.name.startsWith("$it-")}
    })}

    from {
        shade().collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }

    manifest {
        attributes(
                'FMLCorePluginContainsFMLMod': 'true',
                'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
                'TweakOrder': '0',
                'MixinConfigs': 'mixins.panostream.json,mixins.panostream.optifine.json',
                'MixinCompatibilityLevel': 'JAVA_8',
                'FMLAT': 'panostream_at.cfg'
        )
    }
}

defaultTasks 'build'